import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

/*
 * Copyright (C) 2016 the original author or authors.
 * See the NOTICE.md file distributed with this work for additional
 * information regarding copyright ownership.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


group 'de.bitinsomnia'
description 'Embedded WebDAV server to serve the files of a single local folder'



apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply from: "dependencies.gradle"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation commons_lang

    implementation jetty_server
    implementation jetty_servlet
    implementation jetty_servlets

    implementation milton_api
    implementation milton_server

    implementation jcommander

    implementation(slf4j_api_unversioned) {
        version {
            // Milton v3 raised SLF4J API version to a version unsupported by currently used
            // logback version. Forcing downgrade.
            strictly slf4j_version
        }
    }

    implementation javaxServlet

    runtimeOnly logback

    testImplementation junit
}

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Main-Class": "de.bitinsomnia.webdav.server.MiltonStandaloneServer"
        )
    }
    from 'LICENSE.txt'
    from 'NOTICE.md'
}

java {
    withSourcesJar()
    withJavadocJar()
}

task fatJar(type: Jar) {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Main-Class": "de.bitinsomnia.webdav.server.MiltonStandaloneServer"
        )
    }
    archiveClassifier = "fatjar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.assemble.dependsOn "fatJar"

wrapper {
    gradleVersion = "7.4.2"
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/atetzner/webdav-embedded-server")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_REPOSITORY_OWNER")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = 'WebDAV embedded server'
                description = project.description
                url = 'https://github.com/atetzner/webdav-embedded-server'
                inceptionYear = '2016'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'atetzner'
                        name = 'Andreas Tetzner'
                        email = 'info@bit-insomnia.de'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/atetzner/webdav-embedded-server.git'
                    developerConnection = 'scm:git://github.com:atetzner/webdav-embedded-server.git'
                    url = 'https://github.com/atetzner/webdav-embedded-server'
                }
            }
        }
    }
}